buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/'}
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = "${mod_version}-${mod_phase}"
group = 'cloud.lemonslice.solarterms'
archivesBaseName = '24SolarTerms'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: "${mappings_channel}", version: "${mappings_version}"

//    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.console.level', 'info'
            arg "-mixin.config="+ modid +".mixins.json"
            mods {
                solarterms {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.console.level', 'info'
            arg "-mixin.config="+modid+".mixins.json"
            mods {
                solarterms {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            property 'forge.logging.console.level', 'info'

            args '--mod', 'solarterms', '--all', '--output', file('src/generated/resources/')
            arg "-mixin.config="+modid+".mixins.json"
            mods {
                solarterms {
                    source sourceSets.main
                }
            }
        }
    }
}

def version_replaces = [
        'version': project.version
]

processResources {
    duplicatesStrategy = DuplicatesStrategy.FAIL
    filesMatching("META-INF/mods.toml") {
        expand version_replaces
    }
}

def info = [
        "Specification-Title": "24SolarTerms",
        "Specification-Vendor": "Lemon Slice Studio",
        "Specification-Version": "1",
        "Implementation-Title": project.name,
        "Implementation-Version": "${version}",
        "Implementation-Vendor": "Lemon Slice Studio",
        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        "MixinConfigs": "${modid}.mixins.json"
]

repositories {
    maven {
        name = "silveroak"
        url = "https://maven.lemonslice.cloud/releases"
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("cloud.lemonslice.silveroak:SilveroakOutpost-Forge-1.18.1:1.1.5-Beta")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes(info)
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "cloud.lemonslice.solarterms"
            artifactId = "24SolarTerms-Forge-${minecraft_version}"

            artifact reobfArtifact
            pom {
                name = '24SolarTerms for Minecraft 1.18'
                description = 'A Minecraft mod.'
                url = 'https://github.com/lemon-slice-studio/24SolarTerms'
                licenses {
                    license {
                        name = 'BSD-3-Clause'
                        url = 'https://github.com/lemon-slice-studio/24SolarTerms/blob/1.18/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'RoitoLeo'
                        name = 'RoitoLeo'
                    }
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/lemon-slice-studio/24SolarTerms/issues'
                }
                scm {
                    url = 'https://github.com/lemon-slice-studio/24SolarTerms'
                    connection = 'scm:git:git://github.com/lemon-slice-studio/24SolarTerms.git'
                    developerConnection = 'scm:git:git@github.com:lemon-slice-studio/24SolarTerms.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "archive"
            url = "https://maven.lemonslice.cloud/releases"
            credentials {
                username = System.env.MAVEN_USERNAME
                password = System.env.MAVEN_PASSWORD
            }
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        System.env.MAVEN_USERNAME && System.env.MAVEN_PASSWORD
    }
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "${modid}.refmap.json"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}